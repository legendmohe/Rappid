apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def getBaseVersion() {
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        return versionProps['VERSION_CODE']
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

def getVCSVersion() {
    def projectVersion = 'git rev-list --count HEAD'.execute().text.trim()
    return projectVersion
}

ext {
    baseVersion = getBaseVersion()
    version = ext.baseVersion + "." + getVCSVersion()

    println("version:" + version)

    bintrayRepo = 'maven'
    bintrayName = 'Rappid'

    publishedGroupId = 'com.legendmohe.maven'
    libraryName = 'rappid'
    artifact = 'rappid'

    siteUrl = 'https://github.com/legendmohe/Rappid'
    gitUrl = 'https://github.com/legendmohe/Rappid.git'

    libraryDescription = 'For faster android development'
    libraryVersion = version

    developerId = 'legendmohe'
    developerName = 'Xinyu he'
    developerEmail = 'legendmohe@foxmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
        versionCode = baseVersion
        versionName = version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.kbeanie:multipicker:0.9.8@aar'
}

apply from: 'https://raw.githubusercontent.com/attwellBrian/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/attwellBrian/JCenter/master/bintrayv1.gradle'
